plugins {
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

group 'ru.romangr'
version '2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.14

repositories {
    mavenCentral()
}

task createCommitProperties() {
    doLast {
        mkdir 'build/resources/main'
        def buffer = new ByteArrayOutputStream()
        buffer.withWriter("UTF-8", { writer ->
            writer.write('GIT_REVISION=')
        })
        exec {
            standardOutput = buffer
            commandLine 'git', 'log', "--pretty=%h %ci %s", '-1'
        }
        def file = file('build/resources/main/build.properties')
        file.withOutputStream { stream ->
            stream.write(buffer.toByteArray())
        }
    }
    outputs.file('build/resources/main/build.properties')
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.romangr.catbot.Runner'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    dependsOn createCommitProperties
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE'
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion '0.8.4'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [])
        }))
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation('javax.xml:jaxb-api:2.1')
    implementation('org.springframework:spring-web')
    implementation('com.fasterxml.jackson.core:jackson-core')
    implementation('com.fasterxml.jackson.core:jackson-annotations')
    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation('org.apache.commons:commons-lang3')
    implementation('ru.romangr:exceptional:0.2.0')
    implementation('org.slf4j:slf4j-api')
    implementation('ch.qos.logback:logback-classic')
    implementation('com.google.guava:guava:27.1-jre')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.31"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.31"


    compileOnly('org.projectlombok:lombok:1.18.12')
    annotationProcessor('org.projectlombok:lombok:1.18.12')

    testImplementation('org.hamcrest:hamcrest-core')
    testImplementation('org.assertj:assertj-core')
    testImplementation('org.springframework:spring-test')
    testImplementation('org.junit.jupiter:junit-jupiter:5.4.1')
    testImplementation('org.mockito:mockito-core:3.2.4')

}
