plugins {
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'nu.studer.jooq' version '6.0.1'
}

group 'ru.romangr'
version '2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

task createCommitProperties() {
    doLast {
        mkdir 'build/resources/main'
        def buffer = new ByteArrayOutputStream()
        buffer.withWriter("UTF-8", { writer ->
            writer.write('GIT_REVISION=')
        })
        exec {
            standardOutput = buffer
            commandLine 'git', 'log', "--pretty=%h %ci %s", '-1'
        }
        def file = file('build/resources/main/build.properties')
        file.withOutputStream { stream ->
            stream.write(buffer.toByteArray())
        }
    }
    outputs.file('build/resources/main/build.properties')
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.romangr.catbot.Runner'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    dependsOn createCommitProperties
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE'
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion '0.8.8'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [])
        }))
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation('javax.xml:jaxb-api:2.1')
    implementation('org.springframework:spring-web')
    implementation('com.fasterxml.jackson.core:jackson-core')
    implementation('com.fasterxml.jackson.core:jackson-annotations')
    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation('org.apache.commons:commons-lang3')
    implementation('ru.romangr:exceptional:0.4.0')
    implementation('org.slf4j:slf4j-api')
    implementation('ch.qos.logback:logback-classic')
    implementation('com.google.guava:guava:27.1-jre')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.31"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.31"
    implementation 'org.jooq:jooq:3.16.2'
    implementation 'org.jooq:jooq-meta:3.16.2'
    implementation 'org.jooq:jooq-codegen:3.16.2'
    implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
    jooqGenerator 'org.xerial:sqlite-jdbc:3.36.0.3'


    compileOnly('org.projectlombok:lombok:1.18.24')
    annotationProcessor('org.projectlombok:lombok:1.18.24')

    testImplementation('org.hamcrest:hamcrest-core')
    testImplementation('org.assertj:assertj-core')
    testImplementation('org.springframework:spring-test')
    testImplementation('org.junit.jupiter:junit-jupiter:5.4.1')
    testImplementation('org.mockito:mockito-core:4.5.1')

}

jooq {
  version = '3.15.1'

  configurations {
    main {
      generateSchemaSourceOnCompilation = false
      generationTool {
        logging = org.jooq.meta.jaxb.Logging.WARN
        jdbc {
          driver = 'org.sqlite.JDBC'
          url = 'jdbc:sqlite:./catbot_db.sqlite'
          user = 'root'
          password = ''
        }
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.sqlite.SQLiteDatabase'
          }
          generate {
            deprecated = false
            records = false
            immutablePojos = false
            fluentSetters = false
          }
          target {
            packageName = 'ru.romangr.catbot'
            directory = 'src/generated/jooq'
          }
          strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
        }
      }
    }
  }
}
